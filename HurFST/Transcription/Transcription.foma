source ../CharacterClasses/CharacterClasses.foma
define RemoveDiacritics á|à -> a, é|è -> e, í|ì -> i || _ ;
define RemoveSubscripts ₄ -> 0 || _;
define GlideFormation ú -> LabiovelarGlide || [a|e|i] "-" _ .#.|"-" , u _ "-" [a|e|i];
define E e|i;
define U u|ú;
define Triple	{a-a-a} -> ā,	E {-e-} E -> ē,	E {-i-i} -> ī,	{u-u-u} -> ō,	{u-ú-} U -> ū	|| _ ;
define Initial	{a-a} -> ā,		{e-} E -> ē,	{i-} E -> ī,	{u-u} -> ō,		{ú-} U -> ū		|| .#. _ ;
define Left		{a-a} -> ā, 	{e-e} -> ē,		{i-i} -> ī,		{u-u} -> ō,		{ú-} U -> ū		|| "-" _ C;
define Right	{a-a} -> ā, 	E {-e} -> ē,	E {-i} -> ī,	{u-u} -> ō,		{u-ú} -> ū		|| C _ "-";
define Final	{a-a} -> ā, 	{e-e} -> ē,		{i-i} -> ī,		{u-u} -> ō,		{u-ú} -> ū		|| _ .#.;
define Double	{a-a} -> a,		E {-e} -> e,	{i-i} -> i,		{u-u} -> u,		{e-i} -> e		|| _ ;
define Plene Triple .o. Initial .o. Final .o. Right .o. Left;
define VowelContraction Plene .o. Double;
define uReplacement ú -> u || _ ;
define DashRemoval "-" -> 0 || _ ;
define sReplacement s -> c || _ ;
define Devoicing b -> p, d -> t, g -> k, w -> f, z -> s || _ ; 
define Voicing p -> b, t -> d, k -> g, f -> v, s-> z, š -> ž, ḫ -> ġ || V _ V , _ R , R _ , _ .#.;
define Adjustment p -> f || _ f;
define GlideTranscription LabiovelarGlide -> v;
define Transcription RemoveDiacritics .o. RemoveSubscripts.o. GlideFormation .o. VowelContraction .o.
uReplacement .o. DashRemoval .o. sReplacement .o. Devoicing .o. Voicing .o. Adjustment .o.
GlideTranscription;
